%implements "rtipc_tx" "C"

%%
%% This TLC provides the necessary functionality to inline RealTime
%% interprocess communication
%%
%% Copyright (C) 2012
%% Richard Hacker
%% 
%% License: GPL

%include "RtIPC.tlc"

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%function BlockTypeSetup( block, system ) void
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
  %<InitRtIPC()>
%endfunction


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%function BlockInstanceSetup( block, system ) void
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
  %assign tid = LibGetGlobalTIDFromLocalSFcnTID(0)
  %addtorecord block RtIPCHandle InitRtIPCTask(tid)
%endfunction


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%function Start(block, system) Output
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
  %%
  %assign dtype = GetRtIPCDTypeName(LibBlockInputSignalDataTypeId(0))
  %%
  /* Output RT Variable %<Type> Block: %<VarName> 
   * Registering source variable with EtherLAB IO Signal Layer
   */
  rtipc_pdo = rtipc_txpdo(%<RtIPCHandle>, "%<VarName>", %<dtype>, \
            %<LibBlockInputSignalAddr(0,"","",0)>, \
            %<LibBlockInputSignalWidth(0)>);
  %if ISFIELD(block,"ForceOutput")
    %<LibBlockPWork(Pdo,"","",0)> = rtipc_pdo;
  %endif
  if (!rtipc_pdo) {
        %<LibSetRTModelErrorStatus(\
           "\"RealTime IPC Output Signal %<VarName> could not be registered\"")>;
  }
%endfunction


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%function Outputs( block, system ) Output
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
  %%
  %if !ISFIELD(block,"ForceOutput")
    /* %<Type> %<LibGetBlockName(block)>
     * Nothing to do */
    %return
  %endif
  %%
  /* %<Type> %<LibGetBlockName(block)> */
  %%
  if (%<LibBlockIWork(Force,"","",0)> != \
        %<LibBlockParameter(ForceOutput,"","",0)>) {
    const struct txpdo *pdo = \
        (const struct txpdo *)%<LibBlockPWork(Pdo,"","",0)>;

    %<LibBlockIWork(Force,"","",0)> = \
        %<LibBlockParameter(ForceOutput,"","",0)>;
    
    rtipc_set_txpdo_addr(pdo, \
            %<LibBlockIWork(Force,"","",0)> \
            ? %<LibBlockParameterAddr(ForceValue,"","",0)> \
            : NULL);
  }
%endfunction
